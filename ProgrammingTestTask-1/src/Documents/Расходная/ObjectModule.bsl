#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.ОстаткиТоваров.Записать();
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Выручка.Записывать = Истина;

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяТовары.Количество) КАК Количество,
		|	СУММА(РасходнаяТовары.Сумма) КАК Сумма,
		|	МИНИМУМ(РасходнаяТовары.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ДокТЧ
		|ИЗ
		|	Документ.Расходная.Товары КАК РасходнаяТовары
		|ГДЕ
		|	РасходнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОстатки.ОкончаниеСрокаГодности КАК ОкончаниеСрокаГодности,
		|	ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиТоваровОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ПОМЕСТИТЬ ОстаткиТоваров
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						ДокТЧ.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ДокТЧ КАК ДокТЧ)
		|				И ОкончаниеСрокаГодности > &ДатаДокументаНачалоДня) КАК ОстаткиТоваровОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокТЧ.Номенклатура КАК Номенклатура,
		|	ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ДокТЧ.НомерСтроки КАК НомерСтроки,
		|	ДокТЧ.Количество КАК Количество,
		|	ДокТЧ.Сумма КАК Сумма,
		|	ОстаткиТоваров.ОкончаниеСрокаГодности КАК ОкончаниеСрокаГодности,
		|	ЕСТЬNULL(ОстаткиТоваров.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиТоваров.СтоимостьОстаток КАК СтоимостьОстаток
		|ИЗ
		|	ДокТЧ КАК ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиТоваров КАК ОстаткиТоваров
		|		ПО ДокТЧ.Номенклатура = ОстаткиТоваров.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОкончаниеСрокаГодности
		|ИТОГИ
		|	МИНИМУМ(НомерСтроки),
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", 			МоментВремени());
	Запрос.УстановитьПараметр("ДатаДокументаНачалоДня", НачалоДня(Дата));
	Запрос.УстановитьПараметр("Ссылка", 				Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.КоличествоОстаток < ВыборкаНоменклатура.Количество Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара: %1. Остаток %2", ВыборкаНоменклатура.НоменклатураПредставление, ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Поле = СтрШаблон("Товары[%1].Номенклатура", (ВыборкаНоменклатура.НомерСтроки - 1));
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли; 
		
		Если Не Отказ Тогда
			Выборка = ВыборкаНоменклатура.Выбрать();
		
			ОсталосьСписать = ВыборкаНоменклатура.Количество;
			
			Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл

				СписываемКоличество = Мин(Выборка.КоличествоОстаток, ОсталосьСписать);
				Если Выборка.КоличествоОстаток = СписываемКоличество Тогда
					СписываемСтоимость = Выборка.СтоимостьОстаток;
				Иначе
					СписываемСтоимость = Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток * СписываемКоличество;
				КонецЕсли; 
				
				Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
				Движение.Период 				= Дата;
				Движение.Номенклатура 			= Выборка.Номенклатура;
				Движение.ОкончаниеСрокаГодности = Выборка.ОкончаниеСрокаГодности;
				Движение.Количество 			= СписываемКоличество;
				Движение.Стоимость 				= СписываемСтоимость;
				
				ОсталосьСписать = ОсталосьСписать - СписываемКоличество;
				
				Движение = Движения.Выручка.Добавить();
				Движение.Период 		= Дата;
				Движение.Номенклатура 	= Выборка.Номенклатура;
				Движение.Выручка 		= (Выборка.Сумма / Выборка.Количество * СписываемКоличество) - СписываемСтоимость;
				
			КонецЦикла;
			
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти